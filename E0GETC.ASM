;
;	E0GETC.ASM -	ED SC80  get symbol module (OS dependent)
;			RK version (hardware dependent)
;			Uses external PPI_ADR

?Ctrl	equ	40h
?Shift	equ	20h
REP_VAL	equ	50

IFNDEF	PPI_ADR
;EXTRN	PPI_ADR
ENDIF

; Get character from concole (returns in Acc)
GETCHR:
	PUSH	H
	PUSH	D
	PUSH	B
	CALL	GTC
	POP	B
	POP	D
	POP	H
	RET

; Get keyboard status A=0 - not pressed; A=0FFh - key pressed
GETST:
	MVI	A,0
	@out	PPI
	@in	PPI+1
	INR	A
	RZ
	CALL	GTVECT
	ORA	A
	RZ
	LDA	NREP
	ORA	A
	JZ	GETST0
	DCR	A
	MVI	A,0FFH
	RNZ
	LDA	WTREP
	DCR	A
	STA	WTREP
	JZ	GETST1
	XRA	A ; NREP == 1
	STA	NREP
	RET
GETST1:
	MVI	A,2
	STA	NREP
	MVI	A,0FFh
	RET
GETST0:
	MVI	A,REP_VAL
	STA	WTREP
	MVI	A,0FFH
	STA	ET1
	RET

WTREP:	DS	1

GTC:
	MVI	A,REP_VAL
	STA	WTREP

	LHLD	PNT
	MOV	A,M
	CPI	0FFH
	JZ	GTCH0
 	INX	H
	SHLD	PNT
	JMP	??RUS

GTCH0:	MVI	B,REP_VAL
RP3:	PUSH	B
	CALL	GTVECT
	POP	B
	ORA	A
	JZ	RP3
	LDA	NREP
	CPI	1
	JNZ	RP1
	XRA	A
	STA	NREP
	DCR	B
	JNZ	RP3
	INR	A
	STA	NREP
	LXI	H,ET1
	SHLD	PNT
	JMP	GTC
RP1:	LDA	NREP
	ORA	A
	JNZ	RP01
	LXI	H,ET1
RP02:	INX	H
	MOV	A,M
	CPI	0FFH
	JNZ	RP02
	SHLD	PNT
	PUSH	H
	CALL	MDEL
	POP	H
	DCX	H
	MOV	A,M
	JMP	??RUS

RP01:	CALL	MDEL
	LXI	H,ET1
	MOV	A,M
	INX	H
	SHLD	PNT
??RUS:	CPI	0FEH
	RNZ
	@in	PPI+2
	;PUSH	H
	;LHLD	PPI_ADR
	;INX	H
	;INX	H
	;MOV	A,M
	XRI	8
	@out	PPI+2
	;MOV	M,A
	;POP	H
	JMP	GTC

MDEL:
IF	0
	EI
	LXI	H,1000
RP2:	DCX	H
	MOV	A,H
	ORA	L
	JNZ	RP2
	DI
ELSE
	PUSH	B
	LXI	B,0808h
	;PUSH	H
	;LXI	H,PSG1+3
	MVI	A,36h
	@out	PSG1+3
	;MVI	L,0
	MOV	A,C
	;@out	PSG1
	;@out	PSG1
	;MOV	M,C
	;MOV	M,C
RP21:
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	RP21
	;MVI	L,3
	;MVI	M,36
	MVI	A,36h
	@out	PSG1+3
	;POP	H
	POP	B
ENDIF
 	RET

; (ET1) - Pressed keys vector (in order of press)
; (NREP) - Repeat number
; Returns A = 0 - key not pressed A=0FFH - keys pressed
GTVECT:	MVI	B,5
GETC0:	PUSH	B
	LDA	@RD
	ORA	A
	MVI	A,0
	STA	@RD
	JNZ	$+6
	CALL	KBDSCN
	POP	B
	LXI	H,PRESS
	MOV	A,M
	CPI	0FFH
	JNZ	GTV04
	DCR	B
	JNZ	GETC0
	STA	ET1
	XRA	A
	RET
;	JMP	GTVECT

GTV04: 	LXI	H,ET2
	LXI	D,ET1
	PUSH	H
	PUSH	D
GTV08:	LDAX	D
	MOV	M,A
	INX	H
	INX	D
	CPI	0FFH
	JNZ	GTV08
	POP	D
	POP	H

GTV00:	MOV	A,M
	CPI	0FFH
	JZ	GTV06

	LXI	B,PRESS
GTV07:	LDAX	B
	CPI	0FFH
	JZ	GTV01
	CMP	M
	INX	B
	JNZ	GTV07
	MVI	M,0FDH
	STAX	D
	INX	D
	DCX	B
	MVI	A,0FDH
	STAX	B
	INX	B
GTV01:	INX	H
	JMP	GTV00

GTV06:	LXI	B,PRESS
	DCX	B
GTV10:	INX	B
	LDAX	B
	CPI	0FDH
	JZ	GTV10
	CPI	0FFH
	JZ	REP
	DCX	B
GTV11:	INX	B
	LDAX	B
	CPI	0FDH
	JZ	GTV11
	CPI	0FFH
	JZ	GTV22
	STAX	D
	INX	D
	JMP	GTV11
GTV22:	XRA	A
	STA	NREP
GTV21:	MVI	A,0FFH
	STAX	D
	RET

REP:	LDA	NREP
	ORA	A
	JZ	GTV31
	LXI	H,ET2
GTV30:	MOV	A,M
	INX	H
	CPI	0FFH
	JZ	GTV31
	CPI	0FDH
	JZ	GTV30
	XRA	A
	STA	NREP
	JMP	GTV21

GTV31:	LDA	NREP
	INR	A
	JZ	GTV21
	STA	NREP
	JMP	GTV21

; (PRESS) -  Pressed keys vector (in order of discover)
KBDSCN:	LXI	B,40H
	LXI	H,PRESS
	LXI	D,807FH

	PUSH	B
	@IN	PPI+2
	MOV	B,A
	LDA	@RUS
	ANA	B
	JNZ	$+6
	MVI	M,0FEH
	INX	H
	POP	B

NORM0:	CALL	?PRESS
	CZ	FOUND
	INR	B
	DCR	C
	JNZ	NORM0
	MVI	M,0FFH
	RET

?PRESS:	CALL	GETMSK
	MOV	A,E
	@out	PPI
	NOP
	NOP
	NOP
	@in	PPI+1
	ANA	D
	RNZ

	PUSH	H
	LXI	H,200
?PRES0:	@in	PPI+1
	ANA	D
	JNZ	?PRES1
	DCX	H
	MOV	A,H
	ORA	L
	JNZ	?PRES0
?PRES1:	POP	H
	RET


GETMSK:	MOV	A,D
	RLC
	MOV	D,A
	RNC
	MOV	A,E
	RLC
	MOV	E,A
	RET

FOUND:	PUSH	H
	PUSH	D
	PUSH	B
	MOV	A,B
	CPI	3FH
	JNZ	FND01
	MVI	A,20H
	JMP	FND02
FND01:	CPI	10H
	JC	FND03
	ADI	20H
	JMP	FND02
FND03:	MOV	E,B
	MVI	D,0
	LXI	H,KEYBRD
	DAD	D
	MOV	A,M
FND02:	MOV	E,A

	CPI	21H
	CNC	MODCHR
	MOV	A,E
	POP	B
	POP	D
	POP	H
	MOV	M,A
	INX	H
	RET

MODCHR:	MOV	E,A
	CPI	40H
	JNC	MOD4
	CPI	3CH
	JNC	MOD3
MOD4:	@in	PPI+2
	MOV	B,A
	LDA	@CTRL
	ANA	B
	JZ	SCTRL
	@in	PPI+2
	ANI	8
	JNZ	SHIFT0
	@in	PPI+2
	MOV	B,A
	LDA	@SHFT
	ANA	B
	JZ	@SHIFT
	RET

MOD3:	@in	PPI+2
	MOV	B,A
	LDA	@SHFT
	ANA	B
	JNZ	SH3
	RET

SCTRL:	MOV	A,E
	CPI	40H
	RC
	SUI	40H
	MOV	E,A
	RET

SHIFT0:	@in	PPI+2
	MOV	B,A
	LDA	@SHFT
	ANA	B
	JZ	SH5
	MOV	A,E
	CPI	40H
	RC
	CPI	07Fh
	RZ
	ADI	20H
	MOV	E,A
	RET

@SHIFT:	@in	PPI+2
	ANI	8
	RNZ
SH3:	MOV	A,E
	CPI	40H
	JC	SH5
	CPI	7Fh
	jz	SH11
	ADI	20H
	JMP	SH1
SH5:	MOV	A,E
	CPI	40H
	JC	SB
SH1:	MOV	E,A
	RET
SB:	SUI	10H
	CPI	20H
	JNZ	SH1
SH11:
	MVI	E,5Fh
	RET


KEYBRD:	DB	0CH,1FH,1BH,0,1,2,3,4
	DB	9,0AH,0DH
	DB	7FH; Here was 5FH
	DB	8,19H,18H,1AH
; Following map part may be done more compact algoritmically
; Decrease size by 27 bytes
;	DB	'0123456789:;<=>?'
;	DB	'@ABCDEFGHIJKLMNO'
;	DB	'PQRSTUVWXYZ[\]^ '


@RUS:	DB	80H
@CTRL:	DB	?Ctrl
@SHFT:	DB	?Shift
@RD:	DB	0

PNT:	DW	ET1
ET1:	DB	0ffh
	ds	9
	;DSEG
NREP:	DS	1
PRESS:	DS	10
ET2:	DS	10
IFNDEF	noend
;	END
ENDIF


